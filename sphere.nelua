global Sphere = @record{
  center: vec3,
  _radius2: number,
  _invradius: number,
  radius: number,
  mat: Material
}

function Sphere.get_uv(p: vec3): vec2
  local phi = math.atan(p.z, p.x)
  local theta = math.asin(p.y)
  return vec2{1 - ((phi + math.pi) / math.tau), (theta + math.pi/2) / math.pi }
end

function Sphere:update()
  self._invradius = 1 / self.radius
  self._radius2 = self.radius*self.radius
end

function Sphere:hit(r: Ray, tmin: number, tmax: number): (boolean, Hit)
  local oc = r.origin - self.center
  local b = vec3.dot(oc, r.direction)
  local c = vec3.squaredlength(oc) - self._radius2
  local discriminant = b*b - c
  local rec: Hit
  local t: number
  if discriminant > 0 then
    local droot = math.sqrt(discriminant)
    local t1 = -b
    local t2 = droot
    t = t1 - t2
    if t < tmax and t > tmin then
      goto hitted
    end
    t = t1 + t2
    if t < tmax and t > tmin then
      goto hitted
    end
  end
  do return false, rec end
::hitted::
  rec.p = r:pointat(t)
  rec.normal = (rec.p - self.center) * self._invradius
  rec.uv = Sphere.get_uv(rec.normal)
  rec.mat = &self.mat
  rec.t = t
  return true, rec
end
