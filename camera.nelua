
local Camera = @record{
  lower_left_corner: vec3,
  lower_left_corner_direction: vec3,
  horizontal: vec3,
  vertical: vec3,
  origin: vec3,
  lookat: vec3,
  vup: vec3,
  u: vec3,
  v: vec3,
  w: vec3,
  lensradius: float,
  vfov: float,
  aspect: float,
  aperture: float,
  focusdist: float,
  hasaperture: boolean
}

local function Camera_update(self: Camera*)
  local theta = self.vfov*math_pi/180
  local half_height = math_tan(theta/2)
  local half_width = self.aspect * half_height
  self.w = vec3_unit(vec3_sub(self.origin, self.lookat))
  self.u = vec3_unit(vec3_cross(self.vup, self.w))
  self.v = vec3_cross(self.w, self.u)
  self.lower_left_corner = vec3_sub(self.origin,
    vec3_add(vec3_add(vec3_smul(self.u, half_width*self.focusdist),
                      vec3_smul(self.v, half_height*self.focusdist)),
             vec3_smul(self.w, self.focusdist)))
  self.lower_left_corner_direction = vec3_sub(self.lower_left_corner, self.origin)
  self.horizontal = vec3_smul(self.u, 2*half_width*self.focusdist)
  self.vertical = vec3_smul(self.v, 2*half_height*self.focusdist)
  self.lensradius = self.aperture / 2
  self.hasaperture = self.aperture > 0
end

local function Camera_create(lookfrom: vec3, lookat: vec3, vup: vec3,
                             vfov: float, aspect: float, aperture: float, focusdist: float): Camera
  if aperture == 0 then
    focusdist = 1
  end
  local self = Camera{
    origin = lookfrom,
    lookat = lookat,
    vup = vup,
    vfov = vfov,
    aspect = aspect,
    focusdist = focusdist,
    aperture = aperture
  }
  Camera_update(self)
  return self
end

local function Camera_getray(self: Camera*, u: float, v: float)
  local r: Ray
  if self.hasaperture then
    local rd = vec3_random_in_disk(self.lensradius)
    local offset = vec3_add(vec3_smul(self.u, rd.x), vec3_smul(self.v, rd.y))
    r.origin = vec3_add(self.origin, offset)
    r.direction = vec3_sub(vec3_add(self.lower_left_corner, vec3_add(vec3_smul(self.horizontal, u), vec3_smul(self.vertical, v))), r.origin)
  else
    r.origin = self.origin
    r.direction = vec3_add(self.lower_left_corner_direction, vec3_add(vec3_smul(self.horizontal, u), vec3_smul(self.vertical, v)))
  end
  return r
end

local function Camera_rotate(self: Camera*, x: float, y: float)
  local dir = vec3_sub(self.lookat, self.origin)
  dir = vec3_rotvec(dir, self.u, x)
  dir = vec3_rotvec(dir, self.v, y)
  self.lookat = vec3_add(self.origin, dir)
  Camera_update(self)
end

local function Camera_translate(self: Camera*, x: float, y: float, z: float)
  local trans = vec3{0,0,0}
  trans = vec3_add(trans, vec3_smul(self.u, x))
  trans = vec3_add(trans, vec3_smul(self.w,-z))
  trans = vec3_add(trans, vec3_smul(self.v, y))
  self.origin = vec3_add(self.origin, trans)
  self.lookat = vec3_add(self.lookat, trans)
  Camera_update(self)
end
