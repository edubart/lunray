global PERLIN_SIZE <comptime> = 256

global Perlin = @record{}

local perlin: record{
  rands: array(vec3,PERLIN_SIZE),
  permx: array(int,PERLIN_SIZE),
  permy: array(int,PERLIN_SIZE),
  permz: array(int,PERLIN_SIZE)
}

function Perlin.generate()
  for i=0,PERLIN_SIZE-1 do
    perlin.rands[i] = vec3.unit(vec3{2*math.random()-1, 2*math.random()-1, 2*math.random()-1})
  end
end

function Perlin.permute(perm: array(int,PERLIN_SIZE)*)
  for i=0,PERLIN_SIZE-1 do
    perm[i] = (@int)(i)
  end
  for i=PERLIN_SIZE-1,1,-1 do
    local target = (@int)(math.random()*(i+1))
    perm[i], perm[target] = perm[target], perm[i]
  end
end

local function trilinear_interp(c: vec3[2][2][2], u: float, v: float, w: float)
  u, v, w = u*u*(3-2*u), v*v*(3-2*v), w*w*(3-2*w)
  local accum: float = 0
  for i=0,1 do
    for j=0,1 do
      for k=0,1 do
        local weight = vec3{u-i, v-j, w-k}
        accum = accum +
          ((i*u) + (1-i)*(1-u))*
          ((j*v) + (1-j)*(1-v))*
          ((k*w) + (1-k)*(1-w))*
          vec3.dot(c[i][j][k], weight)
      end
    end
  end
  return accum
end

function Perlin.noise(p: vec3): float
  local u, v, w = p.x - math.floor(p.x), p.y - math.floor(p.y), p.z - math.floor(p.z)
  local pi, pj, pk = (@int)(math.floor(p.x)), (@int)(math.floor(p.y)), (@int)(math.floor(p.z))
  local c: vec3[2][2][2]
  for i=0,1 do
    for j=0,1 do
      for k=0,1 do
        c[i][j][k] = perlin.rands[
          perlin.permx[(pi+i) & 255] ~
          perlin.permy[(pj+j) & 255] ~
          perlin.permx[(pk+k) & 255]]
      end
    end
  end
  return trilinear_interp(c, u, v, w)
end

function Perlin.turb(p: vec3, depth: int)
  local accum: float = 0
  local weight: float = 1
  for i=0,depth-1 do
    accum = accum + weight * Perlin.noise(p)
    weight = weight * 0.5
    p = vec3.add(p, p)
  end
  return math.abs(accum)
end

Perlin.generate()
Perlin.permute(perlin.permx)
Perlin.permute(perlin.permy)
Perlin.permute(perlin.permz)
